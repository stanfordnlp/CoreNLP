%
% Context-sensitive mapping of PTB POS tags to
% Universal POS tags.
%
% Author: Sebastian Schuster
% Author: Christopher Manning
%
%
% Context-sensitive mappings
%
%
% TO -> PART (in CONJP phrases)
CONJP < TO=target < VB

relabel target PART

% TO -> PART
VP < VP < (/^TO$/=target <... {/.*/})

relabel target PART

% TO -> ADP (otherwise)
TO=target <... {/.*/}

relabel target ADP

% delete the next two rules, third one should also cover them
%
% VB -> AUX (passive, case 1)
%VP < (/^VB/=target < /^(?i:am|is|are|r|be|being|'s|'re|'m|was|were|been|s|ai|m|art|ar|wase|get|got|getting|gets|gotten)$/ ) < (VP|ADJP [ < VBN|VBD | < (VP|ADJP < VBN|VBD) < CC ] )
%
%relabel target AUX
%
% VB.* -> AUX (passive, case 2)
%SQ|SINV < (/^VB/=target < /^(?i:am|is|are|r|be|being|'s|'re|'m|was|were|been|s|ai|m|art|ar|wase)$/ $++ (VP < VBD|VBN))
%
%relabel target AUX

% VB.* -> AUX (active, case 1)
VP < VP < (/^VB.*$/=target <... {/.*/})

relabel target AUX

% VB -> AUX (active, case 2)
SQ|SINV < (/^VB/=target $++ /^(?:VP|ADJP)/ <... {/.*/})

relabel target AUX

% VB.* -> VERB
/^VB.*/=target <... {/.*/}

relabel target VERB

% IN -> SCONJ (only in case of subordinating conjunctions)
/^SBAR(-[^ ]+)?$/ < (IN=target $++ S|FRAG <... {/.*/})

relabel target SCONJ

% IN -> ADP (otherwise)
IN=target < __

relabel target ADP

% NN -> SYM (in case of the percent sign)
NN=target <... {/\\%/}

relabel target SYM

% NN -> NOUN (otherwise)
NN=target <... {/.*/}

relabel target NOUN

% NFP -> PUNCT (in case of possibly repeated hyphens, asterisks or tildes)
NFP=target <... {/^(~+|\*+|\-+)$/}

relabel target PUNCT

% NFP -> SYM (otherwise)
NFP=target <... {/.*/}

relabel target SYM

% RB -> PART when it is verbal negation (not or its reductions)
@VP|SINV|SQ|FRAG < (RB=target < /^(?i:not|n't|nt|t|n)$/)

relabel target PART

% Otherwise RB -> ADV
RB=target <... {/.*/}

relabel target ADV

%
% 1 to 1 mappings
%
%
% CC -> CONJ
CC=target <... {/.*/}

relabel target CONJ

% CD -> NUM
CD=target <... {/.*/}

relabel target NUM

% DT -> DET
DT=target <... {/.*/}

relabel target DET

% EX -> DET
EX=target <... {/.*/}

relabel target DET

% FW -> X
FW=target <... {/.*/}

relabel target X

% JJ.* -> ADJ
JJ.*=target <... {/.*/}

relabel target ADJ

% LS -> X
LS=target <... {/.*/}

relabel target X

% MD -> AUX
MD=target <... {/.*/}

relabel target AUX

% NNS -> NOUN
NNS=target <... {/.*/}

relabel target NOUN

% NNP -> PROPN
NNP=target <... {/.*/}

relabel target PROPN

% NNPS -> PROPN
NNPS=target <... {/.*/}

relabel target PROPN

% PDT -> DET
PDT=target <... {/.*/}

relabel target DET

% POS -> PART
POS=target <... {/.*/}

relabel target PART

% PRP -> PRON
PRP=target <... {/.*/}

relabel target PRON

% PRP$ -> PRON
/^PRP\$$/=target <... {/.*/}

relabel target PRON

% RBR -> ADV
RBR=target <... {/.*/}

relabel target ADV

% RBS -> ADV
RBS=target <... {/.*/}

relabel target ADV

% RP -> ADP
RP=target <... {/.*/}

relabel target ADP

% UH -> INTJ
UH=target <... {/.*/}

relabel target INTJ

% WDT -> DET
WDT=target <... {/.*/}

relabel target DET

% WP -> PRON
WP=target <... {/.*/}

relabel target PRON

% WP$ -> PRON
/^WP\$$/=target <... {/.*/}

relabel target PRON

% WRB -> ADV
WRB=target <... {/.*/}

relabel target ADV

% `` -> PUNCT
/^``$/=target <... {/.*/}

relabel target PUNCT

% '' -> PUNCT
/^''$/=target < __

relabel target PUNCT

% ( -> PUNCT
/^\($/=target <... {/.*/}

relabel target PUNCT

% ) -> PUNCT
/^\)$/=target <... {/.*/}

relabel target PUNCT

% -LRB- -> PUNCT
/^-LRB-$/=target <... {/.*/}

relabel target PUNCT

% -RRB- -> PUNCT
/^-RRB-$/=target <... {/.*/}

relabel target PUNCT

% , -> PUNCT
/^,$/=target <... {/.*/}

relabel target PUNCT

% . -> PUNCT
/^\.$/=target <... {/.*/}

relabel target PUNCT

% : -> PUNCT
/^:$/=target <... {/.*/}

relabel target PUNCT

% HYPH -> PUNCT
HYPH=target <... {/.*/}

relabel target PUNCT

% # -> SYM
/^#$/=target <... {/.*/}

relabel target SYM

% $ -> SYM.  Also note that there is a no-op rule of SYM -> SYM!
/^\$$/=target <... {/.*/}

relabel target SYM

% ADD -> X
ADD=target <... {/.*/}

relabel target X

% AFX -> X
AFX=target <... {/.*/}

relabel target X

 %GW -> X
GW=target <... {/.*/}

relabel target X

% XX -> X
XX=target <... {/.*/}

relabel target X

