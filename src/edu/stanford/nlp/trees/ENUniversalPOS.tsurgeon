%
% Context-sensitive mapping of PTB POS tags to
% Universal POS tags. 
%
% Author: Sebastian Schuster
%
%
% Context-sensitive mappings
%
%
% TO -> PART (in CONJP phrases)
CONJP < TO=target < VB

relabel target PART

% TO -> PART
VP < VP < (/^TO$/=target <... {/.*/}) 

relabel target PART

% TO -> ADP (otherwise)
TO=target <... {/.*/}

relabel target ADP

% VB -> AUX (passive, case 1)
VP < (/^VB/=target < /^(?i:am|is|are|r|be|being|'s|'re|'m|was|were|been|s|ai|m|art|ar|wase|seem|seems|seemed|seeming|appear|appears|appeared|become|becomes|became|becoming|get|got|getting|gets|gotten|remains|remained|remain)$/ ) < (VP|ADJP [ < VBN|VBD | < (VP|ADJP < VBN|VBD) < CC ] )

relabel target AUX

% VB.* -> AUX (passive, case 2)
SQ|SINV < (/^VB/=target < /^(?i:am|is|are|r|be|being|'s|'re|'m|was|were|been|s|ai|m|art|ar|wase)$/ $++ (VP < VBD|VBN))

relabel target AUX

% VB.* -> AUX (active, case 1)
VP < VP < (/^VB.*$/=target <... {/.*/})

relabel target AUX

% VB -> AUX (active, case 2)
SQ|SINV < (/^VB/=target $++ /^(?:VP|ADJP)/ <... {/.*/})

relabel target AUX

% VB.* -> VERB
/^VB.*/=target <... {/.*/}

relabel target VERB

% IN -> SCONJ (only in case of subordinating conjunctions)
SBAR|SBAR-TMP < (IN=target $++ S|FRAG <... {/.*/})

relabel target SCONJ

% IN -> PREP (otherwise)
IN=target <... {/.*/}

relabel target PREP

% NN -> SYM (in case of the percent sign)
NN=target <... {/\\%/}

relabel target SYM

% NN -> NOUN (otherwise)
NN=target <... {/.*/}

relabel target NOUN

%
% 1 to 1 mappings
%
%
% CC -> CONJ
CC=target <... {/.*/}

relabel target CONJ

% CD -> NUM
CD=target <... {/.*/}

relabel target NUM

% DT -> DET
DT=target <... {/.*/}

relabel target DET

% EX -> DET
EX=target <... {/.*/}

relabel target DET

% FW -> X
FW=target <... {/.*/}

relabel target X

% JJ.* -> ADJ
JJ.*=target <... {/.*/}

relabel target ADJ

% LS -> X
LS=target <... {/.*/}

relabel target X

% MD -> AUX
MD=target <... {/.*/}

relabel target AUX

% NNS -> NOUN
NNS=target <... {/.*/}

relabel target NOUN

% NNP -> PROPN
NNP=target <... {/.*/}

relabel target PROPN

% NNPS -> PROPN
NNPS=target <... {/.*/}

relabel target PROPN

% PDT -> DET
PDT=target <... {/.*/}

relabel target DET

% POS -> PART
POS=target <... {/.*/}

relabel target PART

% PRP -> PRON
PRP=target <... {/.*/}

relabel target PRON

% PRP$ -> PRON
/^PRP\$$/=target <... {/.*/}

relabel target PRON

% RB -> ADV
RB=target <... {/.*/}

relabel target ADV

% RBR -> ADV
RBR=target <... {/.*/}

relabel target ADV

% RBS -> ADV
RBS=target <... {/.*/}

relabel target ADV

% RP -> ADP
RP=target <... {/.*/}

relabel target ADP

% UH -> INTJ
UH=target <... {/.*/}

relabel target INTJ

% WDT -> DET
WDT=target <... {/.*/}

relabel target DET

% WP -> PRON
WP=target <... {/.*/}

relabel target PRON

% WP$ -> PRON
/^WP\$$/=target <... {/.*/}

relabel target PRON

% WRB -> ADV
WRB=target <... {/.*/}

relabel target ADV

% `` -> PUNCT
/^``$/=target <... {/.*/}

relabel target PUNCT

% ! -> PUNCT
/^!$/=target <... {/.*/}

relabel target PUNCT

% # -> SYM
/^#$/=target <... {/.*/}

relabel target SYM

% $ -> SYM
/^\$$/=target <... {/.*/}

relabel target SYM

% ' -> PUNCT
/^'$/=target <... {/.*/}

relabel target PUNCT

% ( -> PUNCT
/^\($/=target <... {/.*/}

relabel target PUNCT

% ) -> PUNCT
/^\)$/=target <... {/.*/}

relabel target PUNCT

% , -> PUNCT
/^,$/=target <... {/.*/}

relabel target PUNCT

% -LRB- -> PUNCT
/^-LRB-$/=target <... {/.*/}

relabel target PUNCT

% -RRB- -> PUNCT
/^-RRB-$/=target <... {/.*/}

relabel target PUNCT

% . -> PUNCT
/^\.$/=target <... {/.*/}

relabel target PUNCT

% : -> PUNCT
/^:$/=target <... {/.*/}

relabel target PUNCT

% ? -> PUNCT
/^\?$/=target <... {/.*/}

relabel target PUNCT

% ADD -> X
ADD=target <... {/.*/}

relabel target X

% AFX -> X
AFX=target <... {/.*/}

relabel target X

 %GW -> X
GW=target <... {/.*/}

relabel target X

% HYPH -> PUNCT
HYPH=target <... {/.*/}

relabel target PUNCT

% NFP -> SYM
NFP=target <... {/.*/}

relabel target SYM

% XX -> X
XX=target <... {/.*/}

relabel target X

