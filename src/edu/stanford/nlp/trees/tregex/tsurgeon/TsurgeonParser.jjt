// jjtree "-OUTPUT_DIRECTORY=src/edu/stanford/nlp/trees/tregex/tsurgeon" src/edu/stanford/nlp/trees/tregex/tsurgeon/TsurgeonParser.jjt
// javacc "-OUTPUT_DIRECTORY=src/edu/stanford/nlp/trees/tregex/tsurgeon" src/edu/stanford/nlp/trees/tregex/tsurgeon/TsurgeonParser.jj

options { UNICODE_INPUT = true;
          SUPPORT_CLASS_VISIBILITY_PUBLIC = false;
          STATIC = false; }

PARSER_BEGIN(TsurgeonParser)

package edu.stanford.nlp.trees.tregex.tsurgeon;

import edu.stanford.nlp.trees.*;
import edu.stanford.nlp.util.Generics;
import java.util.*;


class TsurgeonParser {

  private TreeFactory treeFactory = new LabeledScoredTreeFactory();

  public static void main(String[] args) {
    System.out.println("Reading from standard input...");
    TsurgeonParser t = new TsurgeonParser(System.in);
    try {
      TsurgeonPattern n = t.Root();
      System.out.println(n.toString());
      System.out.println("Thank you.");
    } catch (Exception e) {
      System.out.println("Oops.");
      System.out.println(e.getMessage());
      e.printStackTrace();
    }
  }

}

PARSER_END(TsurgeonParser)


<*> SKIP:
{
  " "
| "\r"
| "\t"
| "\n"
}

<*> TOKEN:
{
  < OPEN_BRACKET: "[" > : DEFAULT
}

TOKEN:
{ < IF: "if" >
| < NOT: "not" >
| < EXISTS: "exists" > : CONDITIONAL
| < DELETE: "delete" > : OPERATION
| < PRUNE:  "prune" > : OPERATION
| < RELABEL: "relabel" > : OPERATION
| < EXCISE: "excise" > : OPERATION
| < INSERT: "insert" > : OPERATION
| < MOVE: "move" > : OPERATION
| < MOVE_PRUNE: "moveprune" > : OPERATION
| < REPLACE: "replace" >  : OPERATION
| < CREATE_SUBTREE: "createSubtree" > : OPERATION
| < ADJOIN: "adjoin" > : OPERATION
| < ADJOIN_TO_HEAD: "adjoinH" > : OPERATION
| < ADJOIN_TO_FOOT: "adjoinF" > : OPERATION
| < COINDEX: "coindex" > : OPERATION
}

<CONDITIONAL> TOKEN:
{ < NAME: ( ~[" ","\n","\r","[","]","(",")",":"] )+ > : DEFAULT }

<OPERATION> TOKEN:
{
  < CLOSE_BRACKET: "]" >
| < SELECTION: "#rightmost" | "#leftmost" >
| < GENERAL_RELABEL: "/" ("\\/"|~["\n","\r"])* "/" ( ("=" (["0"-"9","_","a"-"z","A"-"Z"])+) | ("%" (["0"-"9","_","a"-"z","A"-"Z"])+) | ("/" ("\\/"|"\\]"|"\\\\"|~["\n","\r","]","\\"])* "/") )+ >
| < IDENTIFIER: ~[" ","0"-"9","\n","\r","(","/","|","@","!","#","%","&",")","=","?","[","]",">","<","~","_",".",",","$",":","{","}",";"] ( ~[" ","\n","\r","(","/","@","!","#","%","&",")","=","?","[","]",">","<","~",".",",","$",":"] )* >
| < LOCATION_RELATION: "$-" | "$+" | ">" ("-")? (["0"-"9"])+ >
| < REGEX: "/" ("\\/"|"\\]"|"\\\\"|~["\n","\r","]","\\"])* "/" >
| < QUOTEX: "|" ("\\|"|"\\\\"|~["\n","\r","|","\\"])* "|" >
| < HASH_INTEGER: "#" (["0"-"9"])+ >
| < TREE_NODE_TERMINAL_LABEL: ( ~[" ","\n","\r","[","]","(",")",":"] )+ >
| < TREE_NODE_NONTERMINAL_LABEL: "(" ( ["-","a"-"z","A"-"Z","_","0"-"9","|","@","=","!","#","$","%","^","&","*","'","`","~",">","<","?",",",".","/","{","}","[","]","+","\\"] )* >
| < CLOSE_PAREN: ")" >
}

// TODO: this is wasteful in terms of creating TsurgeonPatternRoot.
// Should separate that out into another production
TsurgeonPatternRoot Root() :
{
  Token name;
  TsurgeonPattern result;
  List<TsurgeonPattern> results = null;
}
{
  result = Operation()
  { return new TsurgeonPatternRoot(result); }
 | LOOKAHEAD(2)
  ( <IF> <EXISTS> name = <NAME> result = Root() )
  { return new TsurgeonPatternRoot(new IfExistsNode(name.image, false, result.children)); }
 | LOOKAHEAD(2)
  ( <IF> <NOT> <EXISTS> name = <NAME> result = Root() )
  { return new TsurgeonPatternRoot(new IfExistsNode(name.image, true, result.children)); }
 |
  ( <OPEN_BRACKET> result = Root() <CLOSE_BRACKET>
    {
      if (results == null) {
        results = Generics.newArrayList();
      }
      for (TsurgeonPattern child : result.children) {
        results.add(child);
      }
    } )+
  {
    TsurgeonPattern[] array = new TsurgeonPattern[results.size()];
    return new TsurgeonPatternRoot(results.toArray(array));
  }
}

TsurgeonPattern Operation() :
{
  TsurgeonPattern child1;
  TsurgeonPattern child2 = null;
  Token newLabel = null;
  TreeLocation loc = null;
  Token operator;
  AuxiliaryTree tree = null;
  List<AuxiliaryTree> treeList = null;
  List<TsurgeonPattern> nodeSelections = null;
  Token regex;
  Token hash_int;
}
{
  operator = <DELETE> nodeSelections = NodeSelectionList(new ArrayList<TsurgeonPattern>())
    { return new DeleteNode(nodeSelections); }
| operator = <PRUNE> nodeSelections = NodeSelectionList(new ArrayList<TsurgeonPattern>())
    { return new PruneNode(nodeSelections); }
| operator = <EXCISE> child1 = NodeSelection() child2 = NodeSelection()
    { return new ExciseNode(child1,child2); }
| LOOKAHEAD(3)
  operator = <RELABEL> child1 = NodeSelection() newLabel = <IDENTIFIER>
    { return new RelabelNode(child1,newLabel.image); }
| LOOKAHEAD(3)
  operator = <RELABEL> child1 = NodeSelection() newLabel = <QUOTEX>
    { return new RelabelNode(child1, newLabel.image); }
| LOOKAHEAD(3)
  operator = <RELABEL> child1 = NodeSelection() regex = <REGEX>
    { return new RelabelNode(child1, regex.image); }
| LOOKAHEAD(3)
  operator = <RELABEL> child1 = NodeSelection() newLabel = <GENERAL_RELABEL>
    { return new RelabelNode(child1, newLabel.image); }
| LOOKAHEAD(3)
  operator = <REPLACE> child1 = NodeSelection() child2 = NodeSelection()
    { return new ReplaceNode(child1,child2); }
| LOOKAHEAD(3)
  operator = <REPLACE> child1 = NodeSelection() treeList = TreeList(false)
    { return new ReplaceNode(child1,treeList); }
| operator = <MOVE> child1 = NodeSelection() loc = Location()
    { return new MoveNode(child1, loc, false); }
| operator = <MOVE_PRUNE> child1 = NodeSelection() loc = Location()
    { return new MoveNode(child1, loc, true); }
| LOOKAHEAD(3)
  operator = <INSERT> child1 = NodeSelection() loc = Location()
    { return new InsertNode(child1, loc); }
| LOOKAHEAD(3)
  operator = <INSERT> tree = TreeRoot(false) loc = Location()
    { return new InsertNode(tree, loc); }
| operator = <CREATE_SUBTREE> tree = TreeRoot(false) nodeSelections = NodeSelectionList(new ArrayList<TsurgeonPattern>())
    { if (nodeSelections.size() == 1) {
        return new CreateSubtreeNode(nodeSelections.get(0), tree);
      } else if (nodeSelections.size() == 2) {
        return new CreateSubtreeNode(nodeSelections.get(0), nodeSelections.get(1), tree);
      } else {
        throw new ParseException("Illegal number of nodes given to createSubtree (" + nodeSelections.size() + ")");
      }
    }
| operator = <ADJOIN> tree = TreeRoot(true) child1 = NodeSelection()
    { return new AdjoinNode(tree, child1); }
| operator = <ADJOIN_TO_HEAD> tree = TreeRoot(true) child1 = NodeSelection()
    { return new AdjoinToHeadNode(tree, child1); }
| operator = <ADJOIN_TO_FOOT> tree = TreeRoot(true) child1 = NodeSelection()
    { return new AdjoinToFootNode(tree, child1); }
| operator = <COINDEX> nodeSelections = NodeSelectionList(new ArrayList<TsurgeonPattern>())
    { return new CoindexNodes(nodeSelections.toArray(new TsurgeonPattern[] {})); }
}

TreeLocation Location() :
{
  Token rel;
  TsurgeonPattern child;
}
{
 rel = <LOCATION_RELATION> child = NodeSelection()
   { return new TreeLocation(rel.image, child); }
}


List<TsurgeonPattern> NodeSelectionList(List<TsurgeonPattern> l) :
{
  TsurgeonPattern result;
}
{
 ( ( result = NodeSelection() ) { l.add(result); }
 ( ( result = NodeSelection() ) {l.add(result); } )* )
   { return l; }
}

// TODO: what does this next comment mean?
// we'll also put in a way to use a SELECTION with a list of nodes.
TsurgeonPattern NodeSelection() :
{
  TsurgeonPattern result;
}
{
  result =  NodeName()
  { return result; }
}

TsurgeonPattern NodeName() :
{
  Token t;
}
{
  t=<IDENTIFIER>
  { return new FetchNode(t.image); }
}

List<AuxiliaryTree> TreeList(boolean requiresFoot) :
{
  List<AuxiliaryTree> trees = Generics.newArrayList();
  AuxiliaryTree tree;
}
{
 ( ( tree = TreeRoot(requiresFoot) ) { trees.add(tree); }
 ( ( tree = TreeRoot(requiresFoot) ) { trees.add(tree); } )* )
   { return trees; }
}


// the argument says whether there must be a foot node on the aux tree.
AuxiliaryTree TreeRoot(boolean requiresFoot) :
{
  Tree t;
}
{
  t = TreeNode()
    { return new AuxiliaryTree(t,requiresFoot); }
}

Tree TreeNode() :
{
 Token label;
 List<Tree> dtrs = null;
}
{
 label = <TREE_NODE_NONTERMINAL_LABEL> dtrs = TreeDtrs(new ArrayList<Tree>())
    { return treeFactory.newTreeNode(label.image.substring(1),dtrs); }
| ( label = <TREE_NODE_TERMINAL_LABEL> )
    { return treeFactory.newTreeNode(label.image,new ArrayList<Tree>()); }
| ( label = <IDENTIFIER> )
    { return treeFactory.newTreeNode(label.image,new ArrayList<Tree>()); }
}

List<Tree> TreeDtrs(List<Tree> dtrs) :
{
  Tree tree;
}
{
  tree = TreeNode() TreeDtrs(dtrs)
    {dtrs.add(0,tree); return dtrs; }
| <CLOSE_PAREN>
  {return dtrs; }
}
